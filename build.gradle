plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.5'
	id 'io.spring.dependency-management' version '1.1.4'
}

group = 'ScreeningHumanity'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
//	set('springCloudVersion', "2023.0.1")
	set('springCloudVersion', "2023.0.0")
	//https://github.com/spring-cloud/spring-cloud-netflix/issues/4275
	//24/07 - 2023.0.1 version 오픈된 문제 있어서 0으로 사용.
}

dependencies {
	//JPA
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	//Validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	//Web
	implementation 'org.springframework.boot:spring-boot-starter-web'

	//Eureka Client
	implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

	//Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	//MySql
	runtimeOnly 'com.mysql:mysql-connector-j'

	//swagger
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'

	//Test Implementation
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	//Query Dsl
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
	annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"

	//Model Mapper
	implementation 'org.modelmapper:modelmapper:3.1.0'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

//QueryDSL_____________________________________
def generated = "src/generated"

tasks.withType(JavaCompile) {
	options.getGeneratedSourceOutputDirectory().set(file(generated))
}

sourceSets {
	main.java.srcDirs += [ generated ]
}

clean {
	delete file(generated)
}
//_____________________________________________
